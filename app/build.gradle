apply plugin: 'com.android.application'
apply from: '../config/quality/quality.gradle'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"
    defaultConfig {
        applicationId "com.alchemist.syncasts"
        minSdkVersion 21
        targetSdkVersion 25
        testInstrumentationRunner "${applicationId}.runner.RxAndroidJUnitRunner"
        versionCode 1
        versionName '0.1.0'
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        debug {
            versionNameSuffix " Debug"
            debuggable true
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    buildTypes.each {
        it.buildConfigField "String", "FIREBASE_ROOT_URL", FirebaseRootUrl
    }

    //Needed because of this https://github.com/square/okio/issues/58
    lintOptions {
        warning 'InvalidPackage'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

ext {
    SUPPORT_LIBRARY_VERSION = '25.4.0'
    RXANDROID_VERSION = '1.2.1'
    RXJAVA_VERSION = '1.2.5'
    SQLBRITE_VERSION = '1.1.1'
    RETROFIT_VERSION = '2.1.0'
    PICASSO_VERSION = '2.5.2'
    OKHTTP_VERSION = '3.6.0'
    EARL_VERSION = '1.2.0'
    TIMBER_VERSION = '4.4.0'
    BUTTERKNIFE_VERSION = '8.4.0'
    DAGGER_VERSION = '2.8'
    DISCRETE_SEEKBAR_VERSION = '1.0.1'
    SLIDING_UP_PANEL_VERSION = '3.3.1'
    CALLIGRAPHY_VERSION = '2.3.0'
    FLEXIBLEADAPTER_VERSION = '5.0.0-rc2'
    LEAKCANARY_VERSION = '1.5'
}

//Solve library version conflict
configurations.all {
    resolutionStrategy {
        force "com.android.support:support-appcompact-v7:$SUPPORT_LIBRARY_VERSION"
        force "com.android.support:design:$SUPPORT_LIBRARY_VERSION"
    }
}

dependencies {

    def daggerCompiler = "com.google.dagger:dagger-compiler:$DAGGER_VERSION"

    // App Dependencies
    compile "com.android.support:appcompat-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:recyclerview-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:design:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:support-annotations:$SUPPORT_LIBRARY_VERSION"

    compile "io.reactivex:rxandroid:$RXANDROID_VERSION"
    compile "io.reactivex:rxjava:$RXJAVA_VERSION"

    compile "com.squareup.sqlbrite:sqlbrite:$SQLBRITE_VERSION"
    compile "com.squareup.retrofit2:retrofit:$RETROFIT_VERSION"
    compile "com.squareup.retrofit2:converter-gson:$RETROFIT_VERSION"
    compile "com.squareup.retrofit2:adapter-rxjava:$RETROFIT_VERSION"
    compile "com.squareup.picasso:picasso:$PICASSO_VERSION"
    compile "com.squareup.okhttp3:okhttp:$OKHTTP_VERSION"
    compile "com.squareup.okhttp3:okhttp-urlconnection:$OKHTTP_VERSION"
    compile "com.squareup.okhttp3:logging-interceptor:$OKHTTP_VERSION"

    compile "com.einmalfel:earl:$EARL_VERSION"

    compile "com.jakewharton.timber:timber:$TIMBER_VERSION"
    compile "com.jakewharton:butterknife:$BUTTERKNIFE_VERSION"
    annotationProcessor "com.jakewharton:butterknife-compiler:$BUTTERKNIFE_VERSION"

    compile "com.google.dagger:dagger:$DAGGER_VERSION"
    annotationProcessor daggerCompiler
    testAnnotationProcessor daggerCompiler
    androidTestAnnotationProcessor daggerCompiler

    debugCompile "com.squareup.leakcanary:leakcanary-android:$LEAKCANARY_VERSION"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$LEAKCANARY_VERSION"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:$LEAKCANARY_VERSION"

    //UI dependencies
    compile "org.adw.library:discrete-seekbar:$DISCRETE_SEEKBAR_VERSION"
    compile "com.sothree.slidinguppanel:library:$SLIDING_UP_PANEL_VERSION"
    compile "uk.co.chrisjenx:calligraphy:$CALLIGRAPHY_VERSION"
    compile "eu.davidea:flexible-adapter:$FLEXIBLEADAPTER_VERSION"
}

// Log out test results to console
tasks.matching { it instanceof Test }.all {
    testLogging.events = ["failed", "passed", "skipped"]
}

apply plugin: 'com.getkeepsafe.dexcount'